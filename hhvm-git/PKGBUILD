# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

_pkgname=hhvm
pkgname=${_pkgname}-git
pkgver=3.3.0
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'libxslt' 'intel-tbb' 'libmcrypt' 'oniguruma' 'curl'
         'libdwarf-git' 'imagemagick' 'libedit')
makedepends=('git' 'cmake' 'gcc' 'boost' 'python2')
source=("git://github.com/facebook/hhvm"
        "git://github.com/hhvm/hhvm-third-party"
        "git://github.com/facebook/folly"
        "git://github.com/facebook/fbthrift"
        'MAX.patch'
        'third-party.patch'
        'hhvm.tmpfile'
        'hhvm@.service'
        'static.mime-types.hdf'
        'default.hdf'
        'server.hdf')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '4cf018eb73210fb39138ba7034ae0bc10ebf4dc39db91a81f2e46d0ff803ea66'
            'f2be28dbc9a337bb1632bdef10c84b80b4ba1ef86ab36a6995e8b835c3883233'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '8681582439385807fd0f8caff780953d3049de93218ac785c48f6df4f4296bce'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '6d48e6bdc546d60a9770d507366ad23702f9997613cbe355d76463e0bf052f68'
            'ba82950161ff38e634841360d7e15cf5314f233ed4d97b02c18e0ba9f7a3f560')
install=hhvm.install
backup=(etc/hhvm/{conf.d/default,conf.d/static.mime-types,server}.hdf)
options+=('!buildflags') # default flags cause libdwarf failed to link

prepare() {
    cd "${srcdir}/${_pkgname}"

    git submodule init
    git config submodule.third-party.url "${srcdir}/hhvm-third-party"
    git submodule update

    cd third-party
    git submodule init
    git config submodule.folly.url "${srcdir}/folly"
    git config submodule.thrift/src.url "${srcdir}/fbthrift"
    git submodule update

    cd "${srcdir}/${_pkgname}"
    patch -p1 -i "$srcdir"/MAX.patch

    msg2 "Remove pcre"
    cd $srcdir/${_pkgname}/third-party
    patch -p1 -i "$srcdir"/third-party.patch
    rm -rv pcre
}

build() {
    cd "$srcdir"/${_pkgname}
    msg2 "Building hhvm"

    # comment for tests
    HPHP_NOTEST=1 \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        .

    make
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "${srcdir}/${_pkgname}"
    make DESTDIR="$pkgdir/" install

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 static.mime-types.hdf "$pkgdir"/etc/hhvm/conf.d/static.mime-types.hdf
    install -Dm644 default.hdf "$pkgdir"/etc/hhvm/conf.d/default.hdf
    install -Dm644 server.hdf "$pkgdir"/etc/hhvm/server.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

