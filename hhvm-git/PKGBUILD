# Maintainer: Yen Chi Hsuan <yan12125 at gmail dot com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

_pkgname=hhvm
pkgname=${_pkgname}-git
pkgver=20150124
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs'
         'fribidi'
         'google-glog'
         'imagemagick'
         'intel-tbb'
         'jemalloc'
         'libdwarf'
         'libedit'
         'libmcrypt'
         'libmemcached'
         'libmysqlclient'
         'libvpx'
         'libxslt'
         'libyaml'
         'oniguruma'
         'pfff'
         'php'
         'sqlite')
# curl included in php
# gd is bundled in hphp/runtime/ext/gd/libgd
# libxml2 included in libxslt
# pcre included in lots of packages
makedepends=('git' 'cmake' 'gcc' 'boost' 'python2')
source=("git+https://github.com/facebook/hhvm"
        "git+https://github.com/hhvm/hhvm-third-party"
        "git+https://github.com/facebook/folly"
        "git+https://github.com/facebook/fbthrift"
        'hhvm.tmpfile'
        'hhvm@.service'
        'server.ini'
        'systemd-server.conf')
backup=('etc/hhvm/server.ini' 'etc/hhvm/systemd-server.conf')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'c049b53f5ca7f8384d3b0a9d257018770b3a171f3b7ae8865cbb2c402a26b5e9'
            'dddf4325926a41b3f1b5a41a1a42364ee463ab627f9d9b05ccf7c4660af3a51c'
            'bca85e0445f3e26df65876c52e507aec8146cd31cce12c82846b55705529caed'
            '66ae41fb6ef07dd02a9c959ab6b6e07bcb1d17b0c164a091fc5ecd9dc7967cd0')

install=hhvm.install
options+=('!buildflags') # default flags cause libdwarf failed to link

_EXTRA_COMPONENTS=('hackificator' 'remove_soft_types')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

prepare() {
    cd "${srcdir}/${_pkgname}"

    git submodule init
    git config submodule.third-party.url "${srcdir}/hhvm-third-party"
    git submodule update

    cd third-party
    git submodule init
    git config submodule.folly.url "${srcdir}/folly"
    git config submodule.thrift/src.url "${srcdir}/fbthrift"
    git submodule update
}

build() {
    cd "${srcdir}/${_pkgname}"
    msg2 "Building hhvm"

    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        .

    make

    cd "${srcdir}/${_pkgname}/hphp/hack/tools"
    msg2 "Building extra components"

    for tool in "${_EXTRA_COMPONENTS[@]}"
    do
        pushd "$tool"
        make clean
        make depend
        make
        popd
    done

    pushd augmented_types
    make clean
    make
    popd
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "${srcdir}/${_pkgname}"
    make DESTDIR="$pkgdir/" install

    msg2 "Install extra components"
    cd "${srcdir}/${_pkgname}/hphp/hack/bin/tools"
    for tool in *
    do
        install -Dm755 "${tool}" "${pkgdir}/usr/bin/${tool}"
    done

    cd "${srcdir}/${_pkgname}/hphp/hack/tools/augmented_types/_build"
    install -Dm755 augmented_types.native "${pkgdir}/usr/bin/augmented_types"

    cd "${srcdir}"
    install -Dm644 hhvm.tmpfile "${pkgdir}/usr/lib/tmpfiles.d/hhvm.conf"
    install -Dm644 hhvm@.service "${pkgdir}/usr/lib/systemd/system/hhvm@.service"
    install -Dm644 server.ini "${pkgdir}/etc/hhvm/server.ini"
    install -Dm644 systemd-server.conf "${pkgdir}/etc/hhvm/systemd-server.conf"

    install -Ddm755 -o http "${pkgdir}/var/log/hhvm"
    install -Ddm755 -o http "${pkgdir}/var/lib/hhvm"

    # HHVM install some files to /usr/lib64, which causes the error "exists in filesystem"
    cd "${pkgdir}/usr"
    mv lib64/* lib/
    rmdir lib64
}
